loadi(63,13); // library
loadi(64,14);

loadi(65,7);  // init

loadi(67,9);  // shell program
loadi(68,10);

loadi(59,3);  // inode table
loadi(60,4);

loadi(62,5);  // root file

loadi(61,2);  // disk free list

// int 0  exception handler
// int 1  timer interrupt 
// int 2  disk controller interrupt
// int 3  console interrupt
// int 4  create/ delete
// int 5  seek/ open/ close
// int 6  read
// int 7  write
// int 8  fork
// int 9  exec
// int 10 exit
// int 11 getpid/ geppid/ wait/ signal
// int 12 logout
// int 13 semget/ semrelease
// int 14 semlock/ semunlock
// int 15 shutdown
// int 16 newusr/ remusr/ setpwd/ getuname/ getuid
// int 17 login

alias i R0;
i=0;
while(i<18) do // load interrupt handlers
    loadi(2+2*i,15+2*i);
    loadi(3+2*i,16+2*i);
    i=i+1;
endwhile;

// mod 0 resource manager
// mod 1 process manager
// mod 2 memory manager
// mod 3 file manager
// mod 4 device manager
// mod 5 scheduler
// mod 6 pager module
// mod 7 boot module

i=0;
while(i<6) do // load modules
    loadi(40+2*i,53+2*i);
    loadi(41+2*i,54+2*i);
    i=i+1;
endwhile;

[TERMINAL_STATUS_TABLE]=0;
[DISK_STATUS_TABLE]=0;

// init/login process

PTBR = PAGE_TABLE_BASE+20;
PTLR = 10;
[PTBR+0]=63;
[PTBR+1]="0100";
[PTBR+2]=64;
[PTBR+3]="0100";
[PTBR+4]=-1;
[PTBR+5]="0000";
[PTBR+6]=-1;
[PTBR+7]="0000";
[PTBR+8]=65;
[PTBR+9]="0100";
[PTBR+10]=-1;
[PTBR+11]="0000";
[PTBR+12]=-1;
[PTBR+13]="0000";
[PTBR+14]=-1;
[PTBR+15]="0000";
[PTBR+16]=66;
[PTBR+17]="0110";
[PTBR+18]=-1;
[PTBR+19]="0000";

[PROCESS_TABLE +16*1] = 0;
[PROCESS_TABLE +16*1 +1]=1;
[PROCESS_TABLE +16*1 +3]=1;
[PROCESS_TABLE +16*1 +4]=CREATED;
[PROCESS_TABLE +16*1 +11]=77;
[PROCESS_TABLE +16*1 +13]=8*512;
[PROCESS_TABLE +16*1 +12]=0;
[PROCESS_TABLE +16*1 +14]=PAGE_TABLE_BASE+20;
[PROCESS_TABLE +16*1 +15]=PTLR;
[66*512]=[65*512+1];

// shell process

PTBR = PAGE_TABLE_BASE+40;
PTLR = 10;
[PTBR+0]=63;
[PTBR+1]="0100";
[PTBR+2]=64;
[PTBR+3]="0100";
[PTBR+4]=-1;
[PTBR+5]="0000";
[PTBR+6]=-1;
[PTBR+7]="0000";
[PTBR+8]=67;
[PTBR+9]="0100";
[PTBR+10]=68;
[PTBR+11]="0100";
[PTBR+12]=-1;
[PTBR+13]="0000";
[PTBR+14]=-1;
[PTBR+15]="0000";
[PTBR+16]=78;
[PTBR+17]="0110";
[PTBR+18]=79;
[PTBR+19]="0110";

[PROCESS_TABLE +16*2] = 0;
[PROCESS_TABLE +16*2 +1]=2;
[PROCESS_TABLE +16*2 +2]=1;
[PROCESS_TABLE +16*2 +3]=1;
[PROCESS_TABLE +16*2 +4]=TERMINATED;
[PROCESS_TABLE +16*2 +11]=80;
[PROCESS_TABLE +16*2 +13]=8*512;
[PROCESS_TABLE +16*2 +12]=0;
[PROCESS_TABLE +16*2 +14]=PAGE_TABLE_BASE+40;
[PROCESS_TABLE +16*2 +15]=PTLR;
[78*512]=[67*512+1];

i=0;
while(i<10) do
    [DISK_MAP_TABLE+2*10+i]=-1;
    i=i+1;
endwhile;

[DISK_MAP_TABLE+2*10+4]=9;
[DISK_MAP_TABLE+2*10+5]=10;

i=496;
while(i<512) do //last 16 words of userpage of init and shell as per-process table initialization
    [77*512+i]=-1; 
    [80*512+i]=-1;
    i=i+1;
endwhile;

i=0;
while(i<32) do // initialization of semaphore table
    [SEMAPHORE_TABLE+4*i]=-1;
    [SEMAPHORE_TABLE+4*i+1]=0;
    i=i+1;
endwhile;

i=3;
while(i<16) do
    [PROCESS_TABLE +16*i +1]=-1;
    [PROCESS_TABLE +16*i +4]=TERMINATED;
    i=i+1;
endwhile;

i=0;
while(i<81) do //0-75 reserved 76-80 idle init processes
    [MEMORY_FREE_LIST + i]=1;
    i=i+1;
endwhile;
while(i<128) do
    [MEMORY_FREE_LIST + i]=0;
    i=i+1;
endwhile;

i=0;
while(i<60) do // initialization of file status table
    [FILE_STATUS_TABLE+4*i]=-1;
    [FILE_STATUS_TABLE+4*i+1]=-1;
    i=i+1;
endwhile;

i=0;
while(i<4) do  // initialization of buffer table
    [BUFFER_TABLE+4*i]=-1;
    [BUFFER_TABLE+4*i+2]=-1;
    [BUFFER_TABLE+4*i+1]=0;
    i=i+1;
endwhile;

i=0;
while(i<32) do  // initialization of open file table
    [OPEN_FILE_TABLE+4*i]=-1;
    [OPEN_FILE_TABLE+4*i+1]=0;
    i=i+1;
endwhile;

[SYSTEM_STATUS_TABLE+3]=0;  //wait_mem_count=0
[SYSTEM_STATUS_TABLE+2]=47; // free pages
return;
